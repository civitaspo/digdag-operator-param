plugins {
    id 'scala'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id "com.diffplug.spotless" version '5.12.4'
}

group = 'pro.civitaspo'
version = '0.0.3'

def digdagVersion = '0.10.0'
def scalaSemanticVersion = "2.13.5"
def depScalaVersion = "2.13"

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://dl.bintray.com/digdag/maven'
    }
}

dependencies {
    implementation group: 'io.digdag', name: 'digdag-spi', version: digdagVersion
    implementation group: 'io.digdag', name: 'digdag-plugin-utils', version: digdagVersion

    // https://mvnrepository.com/artifact/org.scala-lang/scala-library
    implementation group: 'org.scala-lang', name: 'scala-library', version: scalaSemanticVersion
}

dependencyLocking {
    lockMode = LockMode.STRICT
    lockAllConfigurations()
}

shadowJar {
    classifier = null
    dependencies {
        exclude(dependency('io.digdag:.*'))
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

spotless {
    scala {
        scalafmt('2.6.3').configFile('.scalafmt.conf')
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileScala.options.encoding = 'UTF-8'
compileTestScala.options.encoding = 'UTF-8'
compileScala.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

